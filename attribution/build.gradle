import java.text.SimpleDateFormat

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    namespace 'com.affise.attribution'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 4
        versionName "$affiseVersion build (${buildTime()})"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        all {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = jvmVersion
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
    }
}

dependencies {
    //Installreferrer
    implementation "com.android.installreferrer:installreferrer:$installReferrerVersion"

    //Tests
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspressoCore"
    androidTestImplementation "androidx.test.ext:junit:$testAndroidxJunit"
    testImplementation "com.google.truth:truth:$testTruth"
    testImplementation "io.mockk:mockk:$testMockk"
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation "junit:junit:$testJunit"
    testImplementation "org.json:json:$testJson"
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

static def buildTime() {
    def df = new SimpleDateFormat("dd-MM-yyyy HH:mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

apply from: '../publish.gradle'

afterEvaluate {
    publishing {
        publications {
            affiseRelease(MavenPublication) {
                artifactId = 'attribution'
                pom {
                    description = 'Affise Attribution Library'
                }
            }
        }
    }
}
